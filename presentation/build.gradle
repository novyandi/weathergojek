apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+7"))
}

android {
    compileSdkVersion setup.compileSdk
    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode setup.versionCode
        versionName "${setup.versionName}-${setup.versionCodeName}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            dimension = "default"
            applicationId = "${setup.defaultPackageName}.dev"
            resValue "string", "appname", "${setup.appName}-Dev"
            buildConfigField "String", "API_URL", '"https://api.apixu.com/"'
            buildConfigField "String", "API_KEY", '"7065401ba5314155b1551420190106"'
        }
        production {
            dimension = "default"
            applicationId = "${setup.defaultPackageName}"
            resValue "string", "appname", "${setup.appName}"
            buildConfigField "String", "API_URL", '"https://api.apixu.com/"'
            buildConfigField "String", "API_KEY", '"7065401ba5314155b1551420190106"'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith(".apk")) {
                def fileName = outputFile.name.replace("app",
                        "${setup.appName}_${releaseTime()}_${defaultConfig.versionName}")
                outputFileName = fileName
            }
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //UNIT TEST LIBRARY
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.amshove.kluent:kluent:$kluent"

    //KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //ANDROID LIBRARY
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "com.android.support:support-media-compat:$android_support_version"
    implementation "com.android.support:design:$android_support_version"

    //GOOGLE LIBRARY
    implementation "com.google.android.gms:play-services-location:$google_service_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //RX ANDROID LIBRARY
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rx_kotlin_version"

    //OTHER
    implementation 'com.karumi:dexter:5.0.0'
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation project(':domain')
    implementation project(':data')
}
